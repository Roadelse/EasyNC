  !#jj2 for nd in [1,2,3,4,5,6]:
  Subroutine easyO_char_{{ nd }}d(fname, vname, data, type_info, type_ele_alone)
    character(*),intent(in) :: data{{ get_rank_from_nd(nd) }}
    character(*),intent(in) :: fname, vname
    integer,intent(in),optional :: type_info(:, :)
    integer,intent(in),optional :: type_ele_alone

    if (present(type_info)) then  ! shape(data) is necessary due to shape cannot be used in assumed-size array
      if (present(type_ele_alone)) then
        call easyO_char_nd(fname, vname, data, shape(data), type_info, type_ele_alone)
      else
        call easyO_char_nd(fname, vname, data, shape(data), type_info)
      end if
    else
      call easyO_char_nd(fname, vname, data, shape(data))
    end if
  end subroutine easyO_char_{{ nd }}d

  Subroutine easyOA_char_{{ nd }}d(fname, vname, data, type_info, type_ele_alone)
    character(*),allocatable,intent(in) :: data{{ get_rank_from_nd(nd) }}
    character(*),intent(in) :: fname, vname
    integer,intent(in),optional :: type_info(:, :)
    integer,intent(in),optional :: type_ele_alone

    if (allocated(data) .and. size(data) .gt. 0) then
      if (any(lbound(data) .ne. 1)) call easyO(trim(fname), trim(vname)//'.bounds', reshape([lbound(data), ubound(data)], [2, size(shape(data))],order=[2,1]))
      if (present(type_info)) then  ! shape(data) is necessary due to shape cannot be used in assumed-size array
        if (present(type_ele_alone)) then
          call easyO_char_nd(fname, vname, data, shape(data), type_info, type_ele_alone)
        else
          call easyO_char_nd(fname, vname, data, shape(data), type_info)
        end if
      else
        call easyO_char_nd(fname, vname, data, shape(data))
      end if
    end if
  end subroutine easyOA_char_{{ nd }}d
  
  Subroutine easyI_char_{{ nd }}d(fname, vname, data, type_info)
    character(*),intent(out) :: data{{ get_rank_from_nd(nd) }}
    character(*),intent(in) :: fname, vname
    integer,intent(in),optional :: type_info(:, :)

    if (present(type_info)) then  ! shape(data) is necessary due to shape cannot be used in assumed-size array
      call easyI_char_nd(fname, vname, data, shape(data), type_info)
    else
      call easyI_char_nd(fname, vname, data, shape(data))
    end if
  end subroutine easyI_char_{{ nd }}d

  Subroutine easyIA_char_{{ nd }}d(fname, vname, data, type_info)
    character(*),allocatable,intent(inout) :: data{{ get_rank_from_nd(nd) }}
    character(*),intent(in) :: fname, vname
    integer,intent(in),optional :: type_info(:, :)

    if (.not. allocated(data)) then
      call getDims2_enc(fname, vname, dimsT_enc)
      if (hasVar(fname, trim(vname) // '.bounds') .eq. 1) then
        allocate(dimsT2_enc(2, size(dimsT_enc)))
        call easyI(fname, trim(vname) // '.bounds', dimsT2_enc)
        allocate(data({{ get_dimsT2_enc_arrayBound(nd) }}))
        deallocate(dimsT_enc); deallocate(dimsT2_enc)
      else
        allocate(data({{ get_dimsT_enc_arrayShape(nd) }}))
        deallocate(dimsT_enc)
      end if
    end if

    if (present(type_info)) then  ! shape(data) is necessary due to shape cannot be used in assumed-size array
      call easyI_char_nd(fname, vname, data, shape(data), type_info)
    else
      call easyI_char_nd(fname, vname, data, shape(data))
    end if
  end subroutine easyIA_char_{{ nd }}d
!#jj2 endfor


  Subroutine easyO_char_nd(fname, vname, data, dims_data, type_info, type_ele_alone)
    ! vname : name, or name%name
    implicit none
    ! >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> header
    character(*),intent(in) :: data(*)
    character(*),intent(in) :: fname, vname
    integer, intent(in) :: dims_data(:)
    integer, intent(in), optional :: type_info(:, :) ! 2 x n, 0 : type array size, 1 : type position
    integer, intent(in), optional :: type_ele_alone
    
    character(80) :: ovname, strT, posSuffix

    integer,allocatable :: dims(:)
    integer :: ncid, vid, ierr
    integer :: ndims
    
    integer,allocatable :: dids(:)
    integer :: i, j, k, l, m, n
    integer :: data_size, ndims_data, ndims_type
    
    logical :: isExist
    character(40), allocatable :: dimnames(:)

    character(80) :: tname, tvname
    integer,allocatable :: start_index(:), count_lens(:)


    ! >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> body
    ! ================== get dims for both base and type-var mode
    ndims_data = size(dims_data)
    if (present(type_info)) then
      if (present(type_ele_alone)) then   ! save type-element, 1 variable for 1 position
        ndims = ndims_data
        allocate(dims(ndims+1), STAT=ierr)
        call check_enc(ierr, 'allocate')
        dims(1) = len(data)
        dims(2:) = dims_data
      else
        ndims_type = size(type_info(1, :))
        ndims = ndims_data + ndims_type
        allocate(dims(ndims+1), STAT=ierr)
        call check_enc(ierr, 'allocate')
        dims = len(data)
        dims(2:ndims_data+1) = dims_data
        do i = ndims_data + 2, ndims+1
          dims(i) = type_info(1,ndims_type - (i-1-ndims_data) + 1)
        end do
      end if
    else
      ndims = ndims_data
      allocate(dims(ndims+1), STAT=ierr)  ! add length-dim
      call check_enc(ierr, 'allocate')
      dims(1) = len(data)
      dims(2:) = dims_data

    end if

    ovname = vname
    ! ================== get tname and tvname for type-var
    if (present(type_info)) then
      i = scan(vname, '%')
      if (i .eq. 0) then
        stop "Error in easyO_char_nd! please use % to link type and type-var"
      end if
      tname = vname(:i-1)
      tvname = vname(i+1:)
      if (present(type_ele_alone)) then
        posSuffix = '('
        do j = 1, size(type_info(1,:))
          write(strT, '(I0)') type_info(2,j)
          ! print *, 'strT=',strT
          posSuffix = trim(posSuffix)//trim(strT)
          if (j .lt. size(type_info(1,:))) posSuffix = trim(posSuffix)//','
        end do
        posSuffix = trim(posSuffix)//')'
        ovname = trim(tname)//trim(posSuffix)//'%'//trim(tvname)
        ! print *, 'ovname = ',ovname
      end if
    end if

    allocate(dimnames(ndims+1), STAT = ierr)
    if (ierr .ne. 0) then
      stop 1
    end if
    
    allocate(dids(ndims+1), STAT = ierr)
    if (ierr .ne. 0) then
      stop 1
    end if
    
    allocate(start_index(ndims+1), STAT=ierr)
    call check_enc(ierr, 'allocate start_index')
    start_index = 1
    if (present(type_info)) then
      ! print *, 'type_info=',type_info(1,:), type_info(2,:)
      do i = ndims_data + 2, ndims+1
        start_index(i) = type_info(2, ndims_type - (i-1-ndims_data) + 1)
      end do
    end if

    allocate(count_lens(ndims+1), STAT=ierr)
    if (present(type_info)) then
      count_lens(1) = len(data)
      count_lens(2:ndims_data+1) = dims_data
      count_lens(ndims_data+2:) = 1
    else
      count_lens(1) = len(data)
      count_lens(2:) = dims_data
    end if

    data_size = product(dims_data) * len(data)
    
    ! ================== set dimension names
    if (present(type_info)) then
      dimnames(1)=trim(vname)//"_L"
      if (.not. present(type_ele_alone)) then
        do i = 2, ndims_data+1
          write(dimnames(i), '(A, "_d", I1)') trim(tvname), ndims_data-i+2
        end do
        do i = ndims_data + 2, ndims+1
          write(dimnames(i), '(A, "_d", I1)') trim(tname), i-1-ndims_data
        end do
      else
        do i = 2, ndims+1
          write(dimnames(i), '(A, A, "%", A, "_d", I1)') trim(tname), trim(posSuffix), trim(tvname), ndims_data-i+2
        end do
      end if
    else
      dimnames(1)=trim(vname)//"_L"
      do i = 2, ndims+1
        write(dimnames(i), '(A, "_d", I1)') trim(vname), ndims_data-i+2
      end do
    end if
    ! print *, 'dimnames = ', dimnames

    ! =========================== netcdf-relative
    inquire(file = fname, exist = isExist)
    ! print *, "isExist=",isExist
    ! ~~~~~~~~~~~~ open or create nc
    if (isExist) then
      call check_enc( nf90_open(fname, NF90_WRITE, ncid) , "nf90_open")
      call check_enc( nf90_redef(ncid), "nf90_redef")
    else
      call check_enc( nf90_create(fname, NF90_CLOBBER, ncid) , "nf90_create")
    end if
    ! ~~~~~~~~~~~~ get or define dimension
    ! print *, 'dimnames = ', dimnames
    do i = 1, ndims+1
      if (isExist) then
        if (nf90_inq_dimid(ncid, dimnames(i), dids(i)) .ne. nf90_noerr) then
          call check_enc(nf90_def_dim(ncid, dimnames(i), dims(i), dids(i)), "nf90_def_dim")
        end if
      else
        call check_enc(nf90_def_dim(ncid, dimnames(i), dims(i), dids(i)), "nf90_def_dim")
      end if
    end do
    ! ~~~~~~~~~~~~ define variable, or error
    if (nf90_inq_varid(ncid, ovname, vid) .eq. nf90_noerr) then
      if (.not. present(type_info)) then
        write(*,*) 'Error! variable already exists!'
        call check_enc( nf90_close(ncid) , "nf90_close")
        stop 1
      end if
    else
      call check_enc( nf90_def_var(ncid, ovname, NF90_CHAR, dids, vid) , "nf90_def_var")
    end if

    call check_enc( nf90_enddef(ncid) , "nf90_enddef")
    ! ~~~~~~~~~~~~ write data
    ! print *, 'start_index = ',start_index
    ! print *, 'count_lens = ',count_lens
    ! print *, 'dims = ', dims
    call check_enc( nf90_put_var(ncid, vid, data(1:data_size), start=start_index, count=count_lens) , &
      "In easyO_char_nd, nf90_put_var, "//ovname)

    call check_enc( nf90_close(ncid) , "nf90_close")
    
    return
  end subroutine easyO_char_nd



  Subroutine easyO_char(fname, vname, val, type_info)
    implicit none
    ! >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> header
    character(*),intent(in) :: val
    character(*),intent(in) :: fname, vname
    integer, intent(in), optional :: type_info(:, :) ! 2 x n, 0 : type array size, 1 : type position
    
    integer,allocatable :: dims(:)
    
    integer :: ncid, vid, ierr
    
    integer :: i, j, k, l, m, n, did
    
    logical :: isExist
    character(40), allocatable :: dimnames(:)
    integer,allocatable :: dids(:), start_index(:)
    integer :: ndims_type
    character(80) :: tname, tvname
    
    ! >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> body
    ! =========================== netcdf-relative
    inquire(file = fname, exist = isExist)
    ! print *, "isExist=",isExist
    
    if (present(type_info)) then
      ndims_type = size(type_info(1, :))
      allocate(dims(ndims_type+1), STAT=ierr)
      call check_enc(ierr, 'allocate')
      dims(1) = len(val)
      dims(2:) = type_info(1, :)

    ! ================== get tname and tvname for type-var
      i = scan(vname, '%')
      if (i .eq. 0) then
        stop "Error in easyO_char! please use % to link type and type-var"
      end if
      tname = vname(:i-1)
      tvname = vname(i+1:)


      allocate(dimnames(ndims_type+1), STAT = ierr)
      call check_enc(ierr, 'allocate for dimnames')
      ! ================== set dimension names
      dimnames(1) = trim(tname)//"_L"
      do i = 2, ndims_type+1
        write(dimnames(i), '("'//trim(tname)//'", "_d", I1)') i-1
      end do


      allocate(dids(ndims_type+1), STAT = ierr)
      call check_enc(ierr, 'allocate dids')


      allocate(start_index(ndims_type+1), STAT=ierr)
      call check_enc(ierr, 'allocate start_index')
      ! print *, 'type_info=',type_info(1,:), type_info(2,:)
      start_index = 1
      do i = 2, ndims_type+1
        start_index(i) = type_info(2, ndims_type - (i-1) + 1)
      end do
    end if


    ! ~~~~~~~~~~~~ open or create nc
    if (isExist) then
      call check_enc( nf90_open(fname, NF90_WRITE, ncid) , "nf90_open")
      call check_enc(nf90_redef(ncid), "nf90_redef")
    else
      call check_enc( nf90_create(fname, NF90_CLOBBER, ncid) , "nf90_create")
    end if

    ! ~~~~~~~~~~~~ get or define dimension
    if (present(type_info)) then
      do i = 1, ndims_type+1
        if (isExist) then
          if (nf90_inq_dimid(ncid, dimnames(i), dids(i)) .ne. nf90_noerr) then
            call check_enc(nf90_def_dim(ncid, dimnames(i), dims(i), dids(i)), "nf90_def_dim")
          end if
        else
          call check_enc(nf90_def_dim(ncid, dimnames(i), dims(i), dids(i)), "nf90_def_dim")
        end if
      end do

    else
      call check_enc(nf90_def_dim(ncid, vname//'_L', len(val), did), 'nf90_def_dim')
    end if

    ! ~~~~~~~~~~~~ define variable, or error
    if (nf90_inq_varid(ncid, vname, vid) .eq. nf90_noerr) then
      if (.not. present(type_info)) then
        write(*,*) 'Error! variable already exists!'
        call check_enc( nf90_close(ncid) , "nf90_close")
        stop 1
      end if
    else
      if (present(type_info)) then
        call check_enc( nf90_def_var(ncid, vname, NF90_CHAR, dids, vid) , "nf90_def_var")
      else
        call check_enc( nf90_def_var(ncid, vname, NF90_CHAR, did, vid) , "nf90_def_var")
      end if
    end if

    call check_enc( nf90_enddef(ncid) , "nf90_enddef")


    ! ~~~~~~~~~~~~ write val
    if (present(type_info)) then
      call check_enc( nf90_put_var(ncid, vid, val, start=start_index) , "nf90_put_var")
    else
      call check_enc( nf90_put_var(ncid, vid, val) , "nf90_put_var")
    end if

    call check_enc( nf90_close(ncid) , "nf90_close")
    
    return
  end subroutine easyO_char


  Subroutine easyI_char_nd(fname, vname, data, dims_data, type_info)
    implicit none
    ! >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> header
    character(*),intent(out) :: data(*)
    character(*),intent(in) :: fname, vname
    integer, intent(in) :: dims_data(:)
    integer, intent(in), optional :: type_info(:, :) ! 2 x n, 0 : type array size, 1 : type position
    
    character(80) :: ivname, strT, posSuffix, tname, tvname
    integer :: type_ele_alone

    integer,allocatable :: dims(:)
    integer :: ncid, vid, ierr
    integer :: ndims
    
    integer :: i, j, k, l, m, n
    integer :: data_size, ndims_data, ndims_type
    
    logical :: isExist

    integer,allocatable :: start_index(:), count_lens(:)


    ! >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> body
    ! ================== get dims for both base and type-var mode
    ndims_data = size(dims_data)
    ivname = vname
    if (present(type_info)) then
      i = scan(vname, '%')
      if (i .eq. 0) then
        stop "Error in easyI_char_nd! please use % to link type and type-var"
      end if
      tname = vname(:i-1)
      tvname = vname(i+1:)
      if (hasVar(fname, vname) .eq. 1) then
        type_ele_alone = 0
      else
        type_ele_alone = 1
        posSuffix = '('
        do j = 1, size(type_info(1,:))
          write(strT, '(I0)') type_info(2,j)
          ! print *, 'strT=',strT
          posSuffix = trim(posSuffix)//trim(strT)
          if (j .lt. size(type_info(1,:))) posSuffix = trim(posSuffix)//','
        end do
        posSuffix = trim(posSuffix)//')'
        ivname = trim(tname)//trim(posSuffix)//'%'//trim(tvname)
        ! print *, 'ovname = ',ovname
      end if

      if (type_ele_alone .eq. 1) then
        ndims = ndims_data
        allocate(dims(ndims+1), STAT=ierr)
        call check_enc(ierr, 'allocate')
        dims = dims_data
      else
        ndims_type = size(type_info(1, :))
        ndims = ndims_data + ndims_type
        allocate(dims(ndims+1), STAT=ierr)
        call check_enc(ierr, 'allocate')
        dims(1) = len(data)
        dims(2:ndims_data+1) = dims_data
        do i = ndims_data + 2, ndims+1
          dims(i) = type_info(1,ndims_type - (i-1-ndims_data) + 1)
        end do
      end if
    else
      ndims = ndims_data
      allocate(dims(ndims+1), STAT=ierr)
      call check_enc(ierr, 'allocate')
      dims(1) = len(data)
      dims(2:) = dims_data
    end if

    ! ================== get tname and tvname for type-var
    allocate(start_index(ndims+1), STAT=ierr)
    call check_enc(ierr, 'allocate start_index')
    start_index = 1
    if (present(type_info)) then
    ! print *, 'type_info=',type_info(1,:), type_info(2,:)
      if (type_ele_alone .eq. 0) then
        do i = ndims_data + 2, ndims+1
          start_index(i) = type_info(2, ndims_type - (i-1-ndims_data) + 1)
        end do
      end if
    end if

    allocate(count_lens(ndims+1), STAT=ierr)
    if (present(type_info)) then
      if (type_ele_alone .eq. 0) then
        count_lens(1) = len(data)
        count_lens(2:ndims_data+1) = dims_data
        count_lens(ndims_data+2:) = 1
      else
        count_lens(1) = len(data)
        count_lens(2:) = dims_data
      end if
    else
      count_lens(1) = len(data)
      count_lens(2:) = dims_data
    end if

    data_size = product(dims_data) * len(data)
    
    ! =========================== netcdf-relative
    inquire(file = fname, exist = isExist)
    ! print *, "isExist=",isExist
    ! ~~~~~~~~~~~~ open or create nc
    if (isExist) then
      call check_enc(nf90_open(fname, NF90_NOWRITE, ncid) , "nf90_open")
    else
      print *, "Error, file doesn't exist."
      stop "Stopped"
    end if

    ! ~~~~~~~~~~~~ inquire variable
    call check_enc(nf90_inq_varid(ncid, ivname, vid), &
      'in easyI_char_nd, nf90_inq_varid for '//trim(ivname))

    ! ~~~~~~~~~~~~ read data
    ! print *, 'start_index = ',start_index
    ! print *, 'count_lens = ',count_lens
    call check_enc( nf90_get_var(ncid, vid, data(1:data_size), start=start_index, count=count_lens), &
      "in easyI_char_nd, nf90_get_var, "//ivname)

    call check_enc( nf90_close(ncid) , "nf90_close")
    
    return
  end subroutine easyI_char_nd
  


  Subroutine easyI_char(fname, vname, val, type_info)
    ! almost Duplicated with easyI_int
    implicit none
    ! >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> header
    character(*),intent(out) :: val
    character(*),intent(in) :: fname, vname
    integer, intent(in), optional :: type_info(:, :) ! 2 x n, 0 : type array size, 1 : type position
    
    integer,allocatable :: dims(:)
    
    integer :: ncid, vid, ierr, did
    
    integer :: i, j, k, l, m, n
    
    logical :: isExist
    integer,allocatable :: start_index(:)
    integer :: ndims_type
    

    ! >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> body
    ! =========================== netcdf-relative
    ! print *, "isExist=",isExist
    
    if (present(type_info)) then
      ndims_type = size(type_info(1, :))
      allocate(dims(ndims_type+1), STAT=ierr)
      call check_enc(ierr, 'allocate')
      dims(1) = len(val)
      dims(2:) = type_info(1, :)

    ! ================== get tname and tvname for type-var
      allocate(start_index(ndims_type+1), STAT=ierr)
      call check_enc(ierr, 'allocate start_index')
      ! print *, 'type_info=',type_info(1,:), type_info(2,:)
      start_index = 1
      do i = 2, ndims_type+1
        start_index(i) = type_info(2, ndims_type - (i-1) + 1)
      end do
    end if

    inquire(file = fname, exist = isExist)
    ! ~~~~~~~~~~~~ open or create nc
    if (isExist) then
      call check_enc(nf90_open(fname, NF90_NOWRITE, ncid) , "nf90_open")
    else
      print *, "Error, file doesn't exist."
      stop "Stopped"
    end if

    ! ~~~~~~~~~~~~ inquire variable
    call check_enc(nf90_inq_varid(ncid, vname, vid), 'nf90_inq_varid for '//trim(vname))

    ! ~~~~~~~~~~~~ get val
    if (present(type_info)) then
      call check_enc( nf90_get_var(ncid, vid, val, start=start_index) , "in nf90_get_var, "//vname)
    else
      call check_enc( nf90_get_var(ncid, vid, val) , "in nf90_get_var, "//vname)
    end if

    call check_enc( nf90_close(ncid) , "nf90_close")
    
    return
  end subroutine easyI_char
